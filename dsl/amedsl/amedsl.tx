import keywords

// This is the base type, there can only
// be one Program per file
Program:
    statements*=Statement
;

// Defines all the root statements and expressions
// that a program can declare
Statement:
    DefinitionStatement
    | InstantiationStatement
    | TableStatement
;

Comment:
    /\/\/.*$/
;

DefinitionStatement:
    AssetClass
;

InstantiationStatement:
    Asset
;

TableStatement:
    UnitFamiliesTable | UnitsOfMeasureTable
;

UnitFamiliesTable:
    UnitFamilyKeyword TableKeyword name=ID
        unit_families*=UnitFamily
    EndKeyword UnitFamilyKeyword
;

UnitFamily:
    name=ID
    ListSeparatorKeyword? description=STRING?
    LineTerminationKeyword
;

UnitsOfMeasureTable:
    UnitOfMeasureKeyword TableKeyword name=ID
        units*=UnitOfMeasure
    EndKeyword UnitOfMeasureKeyword
;

UnitOfMeasure:
    name=ID 
    (ListSeparatorKeyword symbol=STRING
    ListSeparatorKeyword family=[UnitFamily]
    ListSeparatorKeyword? factor=NUMBER?
    ListSeparatorKeyword? base?=BaseKeyword)#
    LineTerminationKeyword
;

Quantity:
    NUMBER UnitOfMeasureSpecificationSymbol unit=[UnitOfMeasure]
;

Descriptor:
    DescriptorKeyword name=ID
    TypeAssignmentSymbol type=[TypeKeyword]
    description=STRING?
;

DescriptorInstance:
    descriptor=[Descriptor]
    (value=STRING | value=NUMBER | value=Quantity)
;

AssetClass:
    AssetClassKeyword name=ID
        (DescriptionKeyword description=STRING)?
        descriptors*=Descriptor
    EndKeyword AssetClassKeyword
;

Asset:
    AssetKeyword name=ID OfKeyword AssetClassKeyword asset_class=[AssetClass]
        (descriptors*=DescriptorInstance
        (ComponentsKeyword
        OpenListKeyworkd
        components*=[Asset][',']
        CloseListKeyword)?)*
    EndKeyword AssetKeyword
;

