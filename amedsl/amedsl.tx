import keywords

// This is the base type, there can only
// be one Program per file
Program:
    statements*=Statement
;

// Defines all the root statements and expressions
// that a program can declare
Statement:
    DefinitionStatement
    | InstantiationStatement
;

Comment:
    /\/\/.*$/
;

DefinitionStatement:
    AssetClassDefinition
    | TableDefinition
;

TableDefinition:
    UnitFamiliesTableDefinition | UnitsOfMeasureTableDefinition
;

UnitFamiliesTableDefinition:
    UnitFamilyKeyword TableKeyword name=ID
        unit_families*=UnitFamily
    EndKeyword TableKeyword
;

InstantiationStatement:
    AssetInstantiation
;

UnitFamily:
    name=ID
    ListSeparatorKeyword? description=STRING?
    LineTerminationKeyword
;

UnitsOfMeasureTableDefinition:
    UnitOfMeasureKeyword TableKeyword name=ID
        units*=UnitOfMeasure
    EndKeyword TableKeyword
;

UnitOfMeasure:
    name=ID 
    (ListSeparatorKeyword symbol=STRING
    ListSeparatorKeyword family=[UnitFamily]
    ListSeparatorKeyword? factor=NUMBER?
    ListSeparatorKeyword? base?=BaseKeyword)#
    LineTerminationKeyword
;

Quantity:
    '(' value=NUMBER unit=[UnitOfMeasure] ')'
;

Descriptor:
    DescriptorKeyword name=ID
    TypeAssignmentSymbol type=[TypeKeyword]
    description=STRING?
;

HealthIndicator:
    HealthIndicatorKeyword name=ID
    TypeAssignmentSymbol type=[TypeKeyword]
    description=STRING?
;

StressFactor:
    StressFactorKeyword name=ID
    TypeAssignmentSymbol type=[TypeKeyword]
    description=STRING?
;

DescriptorInstance:
    descriptor=[Descriptor]
    (value=STRING | value=NUMBER | value=Quantity)
;

HealthIndicatorInstance:
    health_indicator=[HealthIndicator]
    (value=STRING | value=NUMBER | value=Quantity)
;

StressFactorInstance:
    stress_factor=[HealthIndicator]
    (value=STRING | value=NUMBER | value=Quantity)
;

AssetClassProperty:
    Descriptor | HealthIndicator | StressFactor
;

AssetClassDefinition:
    AssetClassKeyword name=ID
        (DescriptionKeyword description=STRING)?
        properties*=AssetClassProperty
    EndKeyword AssetClassKeyword
;

PropertyInstance:
    DescriptorInstance | HealthIndicatorInstance | StressFactorInstance
;

AssetInstantiation:
    AssetKeyword name=ID OfKeyword AssetClassKeyword asset_class=[AssetClassDefinition]
        (property_values*=PropertyInstance
        (ComponentsKeyword
        OpenListKeyworkd
        components*=[AssetInstantiation][',']
        CloseListKeyword)?)*
    EndKeyword AssetKeyword
;

